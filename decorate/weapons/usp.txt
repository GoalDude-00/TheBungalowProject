actor BPeeestolMag : Ammo //for the player start
{

Inventory.Maxamount 10
+IGNORESKILL
+UNDROPPABLE

}

Actor Pistolpuff : Bulletpuff 
{
		+RANDOMIZE
		+NOEXTREMEDEATH
}

ACTOR BPPistol : BPWeapon replaces Pistol
{
	Weapon.AmmoGive 20
	Weapon.AmmoType "Clip"
	Weapon.Ammotype2 "BPeeestolMag"
	Weapon.Ammogive2 0
	Obituary "$OB_USP"
	+WEAPON.WIMPY_WEAPON
	+WEAPON.NOALERT //added for the reason of reloads
	+WEAPON.ALLOW_WITH_RESPAWN_INVUL
	Inventory.Pickupmessage "$GOT_USP"
	Decal "Bulletchip"
	weapon.slotnumber 2
	Tag "$TAG_USP"
	Radius 20
	Height 16
	States
	{
	Ready:
		BPIS A 1 A_WeaponReady
		Loop
	Deselect:
		BPIS A 1 A_Lower
		BPIS A 0 A_Lower
		Loop
	Select:
		BPIS A 1 A_Raise
		BPIS A 0 A_Raise
		Loop
	Fire:
		TNT1 A 0 A_Jumpifinventory("BPeeestolMag",1,1) //this part will check if the gun is empty and automatically reload if it is 
		goto Altfire
		BPIS A 0 A_AlertMonsters
		BPIS A 0 A_TakeInventory("BPeeestolMag",1)
		BPIS B 2 BRIGHT A_FireBullets (4, 0, 1, 8, "Pistolpuff")
		TNT1 A 0 A_PlaySound("weapons/pistol", CHAN_WEAPON)
		TNT1 A 0 A_GunFlash
		BPIS CDE 1
		BPIS FG 2
		BPIS A 2 A_ReFire
		Goto Ready
	Flash:
		TNT1 B 3 Bright A_Light1
		Goto LightDone

//hurray for gross hacks! courtesy of Joblez
	Altfire:	
		TNT1 A 0 A_JumpIfInventory("Clip",1,1) //don't reload if you don't have any clip ammo left
		Goto Ready
		TNT1 A 0 A_JumpIfInventory("BPeeestolMag",10,"Ready") //don't reload if the gun is full
		BPIR ABCD 1
		TNT1 A 0 A_PlaySound("weapons/pistolr1",CHAN_AUTO)
		BPIR EFGHI 1
		BPIR JKLMN 2
		TNT1 A 0 A_PlaySound("weapons/pistolr2",CHAN_AUTO)
		BPIR OP 1
	Reloadmath: //the logic part
		TNT1 A 0 A_JumpIfInventory("BPeeestolMag", 10, "ReloadFinish") //this is the check for ammo, once it's full it finishes
		TNT1 A 0 A_JumpIfInventory("clip", 1, 1)
		Goto ReloadFinish			
		TNT1 A 0 A_GiveInventory("BPeeestolMag", 1) //these two will add and remove the appropriate amount from clip to weapon magazine
		TNT1 A 0 A_Takeinventory("Clip",1,TIF_NOTAKEINFINITE)
		Goto Reloadmath
		ReloadFinish: //resolves to the end
		BPIR QRST 2
		BPIR UVWY 1
		Goto ready	

 	Spawn:
		PICK B -1
		Stop
	}
}
